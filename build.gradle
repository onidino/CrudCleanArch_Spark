allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group 'com.cleancrud.spark'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenCentral()
    }
}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        csv.enabled = false
        xml.enabled = false
        html.enabled = true
    }
    afterEvaluate { // (optional) : to exclude classes / packages from coverage
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: ['snmaddula/sboot/crud/App.class', 'snmaddula/sboot/util/**'])
        })
    }
}

build.dependsOn jacocoTestReport

configurations {
    childJars
}

subprojects {
    apply from: file('../gradle/tests.gradle')
    apply from: file('../gradle/dependencies.gradle')

    sourceSets {
        test {
            resources {
                srcDir 'resources'
            }
        }
    }
}

dependencies {
    subprojects.each {
        childJars project(it.path)
    }
}


