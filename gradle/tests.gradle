jacoco {
    toolVersion = '0.8.7'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport   // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test   // tests are required to run before generating the report

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        csv.enabled = true
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            "**/config/*",
                            "**/module/*",
                            "**/utils/*",
                            "**/entity/*"
                    ])
        }))
    }
}

//Test coverage Rule to make sure code coverage is 100 %
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}